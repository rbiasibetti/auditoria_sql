USE <NOME_BASE_A_SER_AUDITADA>
GO;

CREATE PROCEDURE SP_AUDITOR 
AS BEGIN 
	SET NOCOUNT ON
	DECLARE @ISQL NVARCHAR(8000) 
	DECLARE @TABLE_BASE NVARCHAR(50)
	DECLARE @TABLE_NAME SYSNAME SELECT @TABLE_NAME = MIN(TABLE_NAME) 
		FROM INFORMATION_SCHEMA.TABLES WHERE TABLE_TYPE= 'BASE TABLE' AND TABLE_NAME NOT IN ('TB_AUDIT') 
		WHILE @TABLE_NAME IS NOT NULL 
		BEGIN 
			SELECT  @TABLE_BASE = DB_NAME()
			EXEC('IF OBJECT_ID (''TRG_AUD_' + @TABLE_NAME + ''', ''TR'') IS NOT NULL DROP TRIGGER TRG_AUD_' + @TABLE_NAME) 
			SET @ISQL = 'CREATE TRIGGER DBO.TRG_AUD_' + @TABLE_NAME + '
					ON  DBO.' + @TABLE_NAME + '
					AFTER INSERT,DELETE,UPDATE
				AS
				BEGIN
					SET NOCOUNT ON
					DECLARE @USR VARCHAR(MAX)
					DECLARE @USRLOCAL VARCHAR(MAX)
					DECLARE @OPERACAO CHAR(1)
					DECLARE @OLD_DATA VARCHAR(MAX)
					DECLARE @NEW_DATA VARCHAR(MAX)
					DECLARE @OLD_DATA_CLS VARCHAR(MAX)
					DECLARE @NEW_DATA_CLS VARCHAR(MAX)

					SELECT @USR = ISNULL((REPLACE(CAST(CONTEXT_INFO() AS VARCHAR(MAX)) COLLATE SQL_LATIN1_GENERAL_CP1_CI_AS, CHAR(0),'''')),(SELECT ''SYSTEM''+''''+PROGRAM_NAME()+''''+DB_NAME()+''''+CAST(CONNECTIONPROPERTY(''CLIENT_NET_ADDRESS'') AS VARCHAR (20))))
					SELECT @USRLOCAL = ISNULL(CAST(SUSER_NAME() AS VARCHAR (MAX)),''ND'')
					
					IF ( (SELECT COUNT(*) FROM INSERTED) > 0 AND (SELECT COUNT(*) FROM DELETED) > 0)
					BEGIN
						SET @OPERACAO = ''U''
					END
					
					IF ( (SELECT COUNT(*) FROM INSERTED) = 0 AND (SELECT COUNT(*) FROM DELETED) > 0)
					BEGIN
						SET @OPERACAO = ''D''
					END
					
					IF ( (SELECT COUNT(*) FROM INSERTED) > 0 AND (SELECT COUNT(*) FROM DELETED) = 0)
					BEGIN
						SET @OPERACAO = ''I''
					END

					IF (@OPERACAO = ''I'')
					BEGIN
						SET @NEW_DATA = (SELECT * FROM INSERTED FOR JSON AUTO)
						SET @OLD_DATA = NULL
						INSERT INTO AUDITORIA..'+@TABLE_BASE+' (DATA_OPERACAO,USUARIO_PC,USUARIO_APP,USUARIO_DB,TABELA,OPERACAO,OLD_DATA,NEW_DATA) VALUES
						(GETDATE(),@USR,HOST_NAME(),@USRLOCAL,'''+ @TABLE_NAME+''',@OPERACAO,@OLD_DATA,@NEW_DATA)
					END
					 
					IF (@OPERACAO = ''D'')
					BEGIN
						SET @NEW_DATA = NULL
						SET @OLD_DATA = (SELECT * FROM DELETED FOR JSON AUTO)
						INSERT INTO AUDITORIA..'+@TABLE_BASE+' (DATA_OPERACAO,USUARIO_PC,USUARIO_APP,USUARIO_DB,TABELA,OPERACAO,OLD_DATA,NEW_DATA) VALUES
						(GETDATE(),@USR,HOST_NAME(),@USRLOCAL,'''+ @TABLE_NAME+''',@OPERACAO,@OLD_DATA,@NEW_DATA)
					END
					 
					IF (@OPERACAO = ''U'')
					BEGIN
						SET @NEW_DATA = (SELECT * FROM INSERTED FOR XML AUTO, BINARY BASE64)
						SET @OLD_DATA = (SELECT * FROM DELETED FOR XML AUTO, BINARY BASE64)
						SET @NEW_DATA_CLS = REPLACE(@NEW_DATA,''<INSERTED '','''')
						SET @OLD_DATA_CLS = REPLACE(@OLD_DATA,''<DELETED '','''')
						SET @NEW_DATA = (SELECT * FROM INSERTED FOR JSON AUTO)
						SET @OLD_DATA = (SELECT * FROM DELETED FOR JSON AUTO)
						IF (@NEW_DATA_CLS <> @OLD_DATA_CLS)
						BEGIN
							INSERT INTO AUDITORIA..'+@TABLE_BASE+' (DATA_OPERACAO,USUARIO_PC,USUARIO_APP,USUARIO_DB,TABELA,OPERACAO,OLD_DATA,NEW_DATA) VALUES
							(GETDATE(),@USR,HOST_NAME(),@USRLOCAL,'''+ @TABLE_NAME+''',@OPERACAO,@OLD_DATA,@NEW_DATA)
						END	
					END
				END' 
				
				----CASO OCORRAM ERROS NA CRIA��O DA TRIGGER, O ERRO � RETORNADO
				EXEC (@ISQL) IF @@ERROR > 0 
					BEGIN 
						PRINT 'TABELA: ' + @TABLE_NAME + '' + @ISQL 
				  END 
					SELECT @TABLE_NAME = MIN(TABLE_NAME) FROM INFORMATION_SCHEMA.TABLES WHERE TABLE_NAME > @TABLE_NAME AND TABLE_TYPE = 'BASE TABLE' AND TABLE_NAME NOT IN ('TB_AUDIT','USUARIOSAUDITORIA','USUARIOSACESSOS','ESTACOES','ESTACOESRSAUDIO','ESTACOESRSFINANCEIRO','JV_USUARIOSAUDITORIA') 
				END 

				----CASO N�O SEJA ENCONTRADA A TABELA COM O NOME DA BASE A SER AUDITADA JUNTO A BASE AUDITORIA, A PROCEDURE A CRIA AUTOMATICAMENTE
     		IF OBJECT_ID('AUDITORIA.DBO.'+@TABLE_BASE) IS NULL
					BEGIN
						EXEC ('USE AUDITORIA
							CREATE TABLE [DBO].['+@TABLE_BASE+'](
								[ID] [BIGINT] IDENTITY(1,1) PRIMARY KEY,
								[DATA_OPERACAO] [DATETIME] NULL,
								[USUARIO_PC] [VARCHAR](200) NULL,
								[USUARIO_APP] [VARCHAR](200) NULL,
								[TABELA] [VARCHAR](100) NULL,
								[OPERACAO] [CHAR](1) NULL,
								[OLD_DATA] [VARCHAR](MAX) NULL,
								[NEW_DATA] [VARCHAR](MAX) NULL,
								[USUARIO_DB] [VARCHAR](100) NULL)')
				END
		END
GO





